<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="18.11.0.1">
<procedure name="main">
<interface/>
<body>
<c>* This example shows the usage of the metrology model</c>
<c>* to measure circles and rectangles with subpixel</c>
<c>* accuracy under challenging conditions easily.</c>
<c>* </c>
<c>* Display initializations</c>
<l>dev_set_line_width (2)</l>
<l>read_image (Image, 'pads')</l>
<l>dev_resize_window_fit_image (Image, 0, 0, -1, -1)</l>
<l>dev_get_window (WindowHandle)</l>
<c></c>
<l>CircleInitRow := [52, 141, 230, 319, 408, 497,52, 141, 230, 319, 408, 497]</l>
<l>CircleInitColumn := [348, 348, 348, 348, 348, 348, 438, 438, 438, 438, 438, 438]</l>
<l>CircleInitRadius := [23, 23, 23, 23, 23, 23, 23, 23, 23, 23, 23, 23]</l>
<l>CircleRadiusTolerance := 12</l>
<l>gen_cross_contour_xld (Cross1, CircleInitRow, CircleInitColumn, 10, rad(45))</l>
<l>gen_circle_contour_xld (ContCircle, CircleInitRow, CircleInitColumn, CircleInitRadius, rad(0), rad(360), 'positive', 1)</l>
<c></c>
<l>RectangleInitRow := [410,410]</l>
<l>RectangleInitColumn := [215,562]</l>
<l>RectangleInitPhi := [0,0]</l>
<l>RectangleInitLength1 := [85,85]</l>
<l>RectangleInitLength2 := [88,88]</l>
<l>RectangleTolerance := 10</l>
<l>gen_rectangle2_contour_xld (Rectangle, RectangleInitRow, RectangleInitColumn, RectangleInitPhi, RectangleInitLength1, RectangleInitLength2)</l>
<c></c>
<l>create_metrology_model (MetrologyHandle)</l>
<l>get_image_size (Image, Width, Height)</l>
<l>set_metrology_model_image_size (MetrologyHandle, Width, Height)</l>
<l>add_metrology_object_rectangle2_measure (MetrologyHandle, RectangleInitRow, RectangleInitColumn, RectangleInitPhi, RectangleInitLength1, RectangleInitLength2, RectangleTolerance, 5, .5, 1, [], [], MetrologyRectangleIndices)</l>
<l>add_metrology_object_circle_measure (MetrologyHandle, CircleInitRow, CircleInitColumn, CircleInitRadius, CircleRadiusTolerance, 5, 1.5, 2, [], [], MetrologyCircleIndices)</l>
<c>* 在一次调用中，每个测量对象可以测量多个圆/矩形/直线/椭圆实例。</c>
<c>* 因为我们喜欢测量每个对象的两个圆，所以我们将'num_instances'设为2。</c>
<l>set_metrology_object_param (MetrologyHandle, MetrologyCircleIndices, 'num_instances', 1)</l>
<c>* 将'measure_transition'设置为'uniform'确保只返回一致的圆，这些圆要么只有从亮到暗的边，要么相反。</c>
<c>* 由于一致性检查会增加运行时间，默认为开关。</c>
<c>* 然而，在这个例子中，打开它会更安全，因为负数和正边都存在。</c>
<l>set_metrology_object_param (MetrologyHandle, MetrologyCircleIndices, 'measure_transition', 'uniform')</l>
<c>* 设置最低分数可以使结果更可靠</c>
<l>set_metrology_object_param (MetrologyHandle, MetrologyCircleIndices, 'min_score', 0.9)</l>
<c></c>
<l>apply_metrology_model (Image, MetrologyHandle)</l>
<c></c>
<l>get_metrology_object_result (MetrologyHandle, MetrologyRectangleIndices, 'all', 'result_type', 'all_param', RectangleParameter)</l>
<c>* Extract the parameters for better readability</c>
<l>Sequence := [0:5:|RectangleParameter| - 1]</l>
<l>RectangleRow := RectangleParameter[Sequence]</l>
<l>RectangleColumn := RectangleParameter[Sequence + 1]</l>
<l>RectanglePhi := RectangleParameter[Sequence + 2]</l>
<l>RectangleLength1 := RectangleParameter[Sequence + 3]</l>
<l>RectangleLength2 := RectangleParameter[Sequence + 4]</l>
<c>* </c>
<c>* Access the results of the circle measurement</c>
<l>get_metrology_object_result (MetrologyHandle, MetrologyCircleIndices, 'all', 'result_type', 'all_param', CircleParameter)</l>
<c>* Extract the parameters for better readability</c>
<l>Sequence := [0:3:|CircleParameter| - 1]</l>
<l>CircleRow := CircleParameter[Sequence]</l>
<l>CircleColumn := CircleParameter[Sequence + 1]</l>
<l>CircleRadius := CircleParameter[Sequence + 2]</l>
<c></c>
<l>stop ()</l>
<l>dev_display (Image)</l>
<l>get_metrology_object_result_contour (Contours, MetrologyHandle, 'all', 'all', 1.5)</l>
<l>get_metrology_object_measures (ToleranceContour, MetrologyHandle, 'all', 'all', Row1, Column1)</l>
<l>gen_cross_contour_xld (Cross, Row1, Column1, 10, rad(45))</l>
<c>* Display everything</c>
<l>dev_set_line_width (1)</l>
<l>dev_set_color ('gray')</l>
<l>dev_display (ToleranceContour)</l>
<l>dev_set_color ('cyan')</l>
<l>dev_display (Cross)</l>
<l>dev_set_line_width (2)</l>
<l>dev_set_color ('green')</l>
<l>dev_display (Contours)</l>
<c></c>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
